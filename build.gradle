import io.gitlab.arturbosch.detekt.Detekt
import io.gitlab.arturbosch.detekt.DetektCreateBaselineTask

// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {

    ext {
        hilt_version = '2.43.2'
        nav_version = '2.5.2'
        lifecycle_version = '2.5.1'
        fragment_version = '1.5.3'
        activity_version = '1.6.0'
        material_version = '1.8.0-alpha01'
        detekt_version = '1.22.0-RC1'
        sqldelight_version = '1.5.4'
        groupie_version = '2.10.1'
        coil_version = '2.1.0'
        sdk_version = 33
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:7.3.1'
        classpath "com.google.dagger:hilt-android-gradle-plugin:$hilt_version"
        classpath "androidx.navigation:navigation-safe-args-gradle-plugin:$nav_version"
        classpath "io.gitlab.arturbosch.detekt:detekt-gradle-plugin:$detekt_version"
        classpath "com.squareup.sqldelight:gradle-plugin:$sqldelight_version"
    }
}

plugins {
    id 'com.android.application' version '7.3.1' apply false
    id 'com.android.library' version '7.3.1' apply false
    id 'org.jetbrains.kotlin.android' version '1.7.20' apply false
    id 'io.gitlab.arturbosch.detekt' version "$detekt_version" apply false
}

allprojects {
    apply plugin: "io.gitlab.arturbosch.detekt"

    detekt {
        toolVersion = detekt_version
        buildUponDefaultConfig = true // preconfigure defaults
        allRules = false // activate all available (even unstable) rules.
        config = files("${project.rootDir}/detekt.yml")
        autoCorrect = true
        // point to your custom config defining rules to run, overwriting default behavior
    }

    dependencies {
        detektPlugins "io.gitlab.arturbosch.detekt:detekt-formatting:$detekt_version"
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

tasks.withType(Detekt).configureEach {
    jvmTarget = "1.8"
}
tasks.withType(DetektCreateBaselineTask).configureEach {
    jvmTarget = "1.8"
}